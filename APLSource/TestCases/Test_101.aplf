 R←Test_101(stopFlag batchFlag);⎕TRAP;logfile;msg
⍝ Start the test service and stop it while pausing
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 logfile←∆HOME,TodaysLogFileName
 FilesAndDirs.DeleteFile logfile

 →T.FailsIf 0=≢msg←∆Execute_SC_Cmd'start'
 ∆Delay 1

 →T.FailsIf 0=≢msg←∆Execute_SC_Cmd'query'
 →T.FailsIf~∨/'RUNNING'⍷,{⍵⌿⍨∨/'STATE'⍷⎕FMT ⍵}↑msg

 →T.FailsIf 0=≢msg←∆Execute_SC_Cmd'pause'
 ∆Delay 2

 →T.FailsIf 0=≢msg←∆Execute_SC_Cmd'query'
 →T.FailsIf~∨/'PAUSED'⍷,{⍵⌿⍨∨/'STATE'⍷⎕FMT ⍵}↑msg

 →T.FailsIf 0=≢msg←∆Execute_SC_Cmd'stop'
 ∆Delay 2

 →T.FailsIf 0=≢msg←∆Execute_SC_Cmd'query'
 →T.FailsIf~∨/'STOPPED'⍷,{⍵⌿⍨∨/'STATE'⍷⎕FMT ⍵}↑msg

 R←T._OK
