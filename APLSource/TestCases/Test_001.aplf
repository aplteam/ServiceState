 R←Test_001(stopFlag batchFlag);⎕TRAP;ps;qdmx
⍝ Test the Init function
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 ##.ServiceState.Init ⍬
 →T.FailsIf 5≠##.ServiceState.timeout
 →T.FailsIf''≢##.ServiceState.logFunction

 ps←##.ServiceState.CreateParmSpace
 ps.timeout←20
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.⎕FX'{r}←Log msg' 'r←0⍴⎕←msg'
 ps.logFunction←'Log'
 ps.logFunctionParent←#.TEMP
 ##.ServiceState.Init ps
 →T.FailsIf ##.ServiceState.timeout≠20
 →T.FailsIf ##.ServiceState.logFunction≢'Log'
 →T.FailsIf ##.ServiceState.logFunctionParent≢#.TEMP

 ps←##.ServiceState.CreateParmSpace
 ps.notAllowed←1
 :Trap 11
     ##.ServiceState.Init ps
     →T.FailsIf 1
 :Else
     qdmx←⎕DMX
     →T.FailsIf'Invalid parameter'≢qdmx.EM
 :EndTrap

 R←T._OK
