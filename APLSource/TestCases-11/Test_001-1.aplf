 R←Test_001(stopFlag batchFlag);⎕TRAP;ps
⍝ Test the Init function
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 ##.ServiceState.Init ⍬
 →FailsIf 5≠##.ServiceState.timeout
 →FailsIf''≢##.ServiceState.logFunction

 ps←##.ServiceState.CreateParmSpace
 ps.timeout←20
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.⎕FX'{r}←Log msg' 'r←0⍴⎕←msg'
 ps.logFunction←'Log'
 ps.logFunctionParent←#.TEMP
 ##.ServiceState.Init ps
 →FailsIf ##.ServiceState.timeout≠20
 →FailsIf ##.ServiceState.logFunction≢'Log'
 →FailsIf ##.ServiceState.logFunctionParent≢#.TEMP

 ps←##.ServiceState.CreateParmSpace
 ps.notAllowed←1
 :Trap 0
     ##.ServiceState.Init ps
     →FailsIf 1
 :Else
     →FailsIf'Invalid parameter'≢0⊃⎕DM
 :EndTrap

 R←∆OK
